<!DOCTYPE html>
<html lang="en">
<head>
    <title>Microfacet reflectance</title>
    <meta charset="utf-8">
    <meta name="viewport" content="width=device-width, user-scalable=no, minimum-scale=1.0, maximum-scale=1.0">
    <style>
        body {
            font-family: Monospace;
            background-color: #707080;
            margin: 0px;
            overflow: hidden;
        }
    </style>
</head>
<body>
<div id="container">
    <div id="controlBar" style="text-align: center;">
        <input type="file" id="meshInputFile" style="display: none;" onchange="loadFile(this.files[0])">
        <input type="button" id="meshInputButton" value="Select OBJ File" onclick="document.getElementById('meshInputFile').click()">
        <input type="file" id="texInputFile" style="display: none;" onchange="loadTexture(this.files[0], 'diffuseTexture')">
        <input type="button" id="texInputButton" value="Select Texture File" onclick="document.getElementById('texInputFile').click()">
        <label><input type="checkbox" id="showAxesCheckbox" onchange="toggleAxes(this.checked)" checked="true">Show Axes</label>
        <label><input type="checkbox" id="showWireframeCheckbox" onchange="toggleWireframe(this.checked)">Show Wireframe</label>
        <label><input type="checkbox" id="showNormalsCheckbox" onchange="toggleNormals(this.checked)">Show Normals</label>
        <label><input type="checkbox" id="fixLightsToCameraCheckbox" onchange="toggleFixLightsToCamera(this.checked)">Fix lights to camera</label>
        <br>
        Normal length:
        <input type="range" id="normalLengthRange" min="0.02" max="0.5" step="0.02" value="0.3" oninput="setNormalLength(this.value)">
        Exposure:
        <input type="range" id="exposureRange" min="10.0" max="18.0" step="0.5" value="15.0" oninput="setUniformLog('exposure', this.value)">
        Alpha:
        <input type="range" id="alphaRange" min="-4.0" max="4.0" step="0.25" value="0.7" oninput="setUniformLog('alpha', this.value)">
        <br>
        Left click + drag to orbit. Right click + drag to pan. Scroll to zoom.
    </div>
</div>

<script src="js/three.min.js"></script>
<script src="js/BufferGeometryUtils.js"></script>
<script src="js/Detector.js"></script>
<script src="js/OrbitControls.js"></script>
<script src="js/OBJLoader.js"></script>

<!-- VERTEX SHADER -->
<script id="vertexShader" type="x-shader/x-vertex">
varying vec3 vNormal;
varying vec3 vPosition;
varying vec2 vUv;

void main() {
    vec4 posEye = modelViewMatrix * vec4(position, 1.0);
    vPosition = posEye.xyz;
    vNormal = normalize(normalMatrix * normal);
    vUv = uv;
    gl_Position = projectionMatrix * posEye;
}
</script>

<!-- FRAGMENT SHADER -->
<script id="fragmentShader" type="x-shader/x-fragment">
#define M_PI 3.141592653589793238

uniform vec3 lightColors[NUM_LIGHTS];
uniform vec3 lightPositions[NUM_LIGHTS];

uniform float exposure;
uniform float alpha;

uniform sampler2D diffuseTexture;

varying vec3 vNormal;
varying vec3 vPosition;
varying vec2 vUv;

float computeF(vec3 L, vec3 H) {
    float ni = 1.0, nt = 2.0;
    float c = abs(dot(L, H));
    float gSquared = (nt * nt) / (ni * ni) - 1.0 + c * c;
    float F = 1.0;
    if (gSquared >= 0.0) {
        float g = sqrt(gSquared);
        F = 0.5 * pow((g - c) / (g + c), 2.0);
        F *= 1.0 + pow((c * (g + c) - 1.0) / (c * (g - c) + 1.0), 2.0);
    }
    return F;
}

float computeD(vec3 N, vec3 H) {
    if (dot(H, N) <= 0.0) return 0.0;
    float cosThetaM = dot(H, N);
    float cos2 = cosThetaM * cosThetaM;
    float sin2 = 1.0 - cos2;
    float tan2 = sin2 / cos2;
    return exp(-tan2 / (alpha * alpha)) / (M_PI * alpha * alpha * cos2 * cos2);
}

float gHelper(vec3 I, vec3 H, vec3 N) {
    float G = 0.0;
    float vm = dot(I, H);
    float vn = dot(I, N);
    if (vm / vn > 0.0) {
        float cosV = dot(normalize(I), normalize(N));
        float sinV = sqrt(1.0 - cosV * cosV);
        float tanV = sinV / cosV;
        float a = 1.0 / (alpha * tanV);
        if (a > 1.6) {
            G = 1.0;
        } else {
            G = (3.535 * a + 2.181 * a * a) / (1.0 + 2.276 * a + 2.577 * a * a);
        }
    }
    return G;
}

float computeG(vec3 N, vec3 H, vec3 L, vec3 V) {
    return gHelper(L, H, N) * gHelper(V, H, N);
}

vec3 to_sRGB(vec3 c) { return pow(c, vec3(1.0 / 2.2)); }
vec3 from_sRGB(vec3 c) { return pow(c, vec3(2.2)); }

void main() {
    vec3 V = normalize(-vPosition);
    vec3 N = normalize(vNormal);
    vec3 baseColor = from_sRGB(texture2D(diffuseTexture, vUv).rgb);
    vec3 finalColor = vec3(0.0);

    for (int i = 0; i < NUM_LIGHTS; i++) {
        vec3 L = normalize(lightPositions[i] - vPosition);
        vec3 H = normalize(L + V);
        float r2 = dot(lightPositions[i] - vPosition, lightPositions[i] - vPosition);

        float F = computeF(L, H);
        float D = computeD(N, H);
        float G = computeG(N, H, L, V);

        float NdotL = max(dot(N, L), 0.0);
        float NdotV = max(dot(N, V), 0.0);
        float denom = max(4.0 * NdotL * NdotV, 0.001);

        vec3 spec = vec3(F * D * G / denom);
        vec3 diff = baseColor / M_PI;

        finalColor += lightColors[i] * (diff + spec) * NdotL / r2;
    }

    gl_FragColor = vec4(to_sRGB(finalColor * exposure), 1.0);
}
</script>

<script src="js/A4Common.js"></script>
<script>
    commonInit(false);
    init();
    animate();

    function init() {
        defaultTextureTarget = 'diffuseTexture';
        meshMaterial.uniforms.diffuseTexture = { type: 't', value: null };

        addLight(new THREE.Vector3(50, 50, 100), new THREE.Vector3(0.8, 0.8, 0.8));
        addLight(new THREE.Vector3(-50, -50, -50), new THREE.Vector3(0.5, 0.5, 0.6));

        document.getElementById('alphaRange').oninput();
    }
</script>
</body>
</html>
